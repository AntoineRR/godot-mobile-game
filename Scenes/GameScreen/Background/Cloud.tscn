[gd_scene load_steps=4 format=2]

[ext_resource path="res://Assets/Backgrounds/texture_shader.png" type="Texture" id=1]

[sub_resource type="Shader" id=1]
code = "shader_type canvas_item;

uniform int pixel_amount;
uniform float cloudscale:hint_range(0.0, 5.0);
uniform float randomness:hint_range(0.0,50000.0);
uniform float speed:hint_range(-1.0, 1.0);
uniform int OCTAVES:hint_range(1,10);

uniform float threshold1:hint_range(0.0, 1.0);
uniform vec4 color1:hint_color;
uniform float threshold2:hint_range(0.0, 2.0);
uniform vec4 color2:hint_color;
uniform float threshold3:hint_range(0.0, 2.0);
uniform vec4 color3:hint_color;

const mat2 m = mat2( vec2(1.6,  1.2), vec2(-1.2,  1.6) );

vec2 hash( vec2 p ) {
	p = vec2(dot(p,vec2(127.1,311.7)), dot(p,vec2(269.5,183.3)));
	return -1.0 + 2.0*fract(sin(p)*randomness);
}

float noise( in vec2 p ) {
    const float K1 = 0.366025404; // (sqrt(3)-1)/2;
    const float K2 = 0.211324865; // (3-sqrt(3))/6;
	vec2 i = floor(p + (p.x+p.y)*K1);	
    vec2 a = p - i + (i.x+i.y)*K2;
    vec2 o = (a.x>a.y) ? vec2(1.0,0.0) : vec2(0.0,1.0); //vec2 of = 0.5 + 0.5*vec2(sign(a.x-a.y), sign(a.y-a.x));
    vec2 b = a - o + K2;
	vec2 c = a - 1.0 + 2.0*K2;
    vec3 h = max(0.5-vec3(dot(a,a), dot(b,b), dot(c,c) ), 0.0 );
	vec3 n = h*h*h*h*vec3( dot(a,hash(i+0.0)), dot(b,hash(i+o)), dot(c,hash(i+1.0)));
    return dot(n, vec3(70.0));	
}

vec4 mask(vec2 uvPos) {
	vec4 mask = vec4(1.0,1.0,1.0,0);
	if (uvPos.y < 0.5) {
		mask.a = clamp(mask.a + uvPos.y*4.0, 0.0, 1.0);
	}
	else {
		mask.a = clamp(mask.a + (1.0-(uvPos.y-0.5)*2.0)*2.0, 0.0, 1.0);
	}
	return mask;
}

float fbm(vec2 uv, float initial_amplitude, float amplitude_factor, float time) {
	float total = 0.0, amplitude = initial_amplitude;
	for (int i = 0; i < OCTAVES; i++) {
		total += abs(noise(uv + vec2(sin(time))) * amplitude);
		uv = m * uv;
		amplitude *= amplitude_factor;
	}
	return total;
}

vec4 apply_threshold(vec4 col, vec2 uv) {
	if (col.a > threshold1) {
		if (col.a + (1.0 - uv.y)*0.9 > threshold2) {
			if (col.a + 1.9*(1.0 - uv.y)*0.9 > threshold3) {
				col = vec4(color3.rgb, 1.0);
			}
			else {
				col = vec4(color2.rgb, 1.0);
			}
		}
		else {
			col = vec4(color1.rgb, 1.0);
		}
	}
	else {
		col = vec4(0,0,0,0);
	}
	return col;
}

void fragment() {
	vec2 ps = TEXTURE_PIXEL_SIZE;
    vec2 ratio = vec2(1, ps.x / ps.y);
	vec2 pixels = vec2(float(pixel_amount)) * ratio;
	vec2 grid_uv = round(UV * pixels) * ratio / pixels;
    float time = TIME * speed;
    float q = cloudscale;
    
    //ridged noise shape
	vec2 uv = grid_uv * cloudscale;
    uv.x -= q - time;
    float r = fbm(uv, 0.8, 0.7, time);
    
	vec4 col = vec4(r*r) * mask(grid_uv) + 0.5*mask(grid_uv);
	col = apply_threshold(col, grid_uv);

	COLOR = col;
	//COLOR = mask(grid_uv);
}
"

[sub_resource type="ShaderMaterial" id=2]
shader = SubResource( 1 )
shader_param/pixel_amount = 540
shader_param/cloudscale = 1.213
shader_param/randomness = 27368.4
shader_param/speed = -0.074
shader_param/OCTAVES = 7
shader_param/threshold1 = 0.758
shader_param/color1 = Color( 0.227451, 0.184314, 0.0823529, 1 )
shader_param/threshold2 = 1.251
shader_param/color2 = Color( 0.545098, 0.427451, 0.196078, 1 )
shader_param/threshold3 = 1.882
shader_param/color3 = Color( 0.792157, 0.721569, 0.435294, 1 )

[node name="Sprite" type="Sprite"]
material = SubResource( 2 )
texture = ExtResource( 1 )
