[gd_scene load_steps=4 format=2]

[ext_resource path="res://Assets/Backgrounds/texture_shader.png" type="Texture" id=1]

[sub_resource type="Shader" id=4]
code = "shader_type canvas_item;

uniform int pixel_amount;
uniform float cloudscale:hint_range(0.0, 5.0);
uniform float randomness:hint_range(0.0,50000.0);
uniform float speed:hint_range(-1.0, 1.0);

uniform float threshold1:hint_range(0.0, 1.0);
uniform vec4 color1:hint_color;
uniform float threshold2:hint_range(0.0, 2.0);
uniform vec4 color2:hint_color;
uniform float threshold3:hint_range(0.0, 2.0);
uniform vec4 color3:hint_color;

const mat2 m = mat2( vec2(1.6,  1.2), vec2(-1.2,  1.6) );

vec2 hash( vec2 p ) {
	p = vec2(dot(p,vec2(127.1,311.7)), dot(p,vec2(269.5,183.3)));
	return -1.0 + 2.0*fract(sin(p)*randomness);
}

float noise( in vec2 p ) {
    const float K1 = 0.366025404; // (sqrt(3)-1)/2;
    const float K2 = 0.211324865; // (3-sqrt(3))/6;
	vec2 i = floor(p + (p.x+p.y)*K1);	
    vec2 a = p - i + (i.x+i.y)*K2;
    vec2 o = (a.x>a.y) ? vec2(1.0,0.0) : vec2(0.0,1.0); //vec2 of = 0.5 + 0.5*vec2(sign(a.x-a.y), sign(a.y-a.x));
    vec2 b = a - o + K2;
	vec2 c = a - 1.0 + 2.0*K2;
    vec3 h = max(0.5-vec3(dot(a,a), dot(b,b), dot(c,c) ), 0.0 );
	vec3 n = h*h*h*h*vec3( dot(a,hash(i+0.0)), dot(b,hash(i+o)), dot(c,hash(i+1.0)));
    return dot(n, vec3(70.0));	
}

float fbm(vec2 n) {
	float total = 0.0, amplitude = 0.1;
	for (int i = 0; i < 7; i++) {
		total += noise(n) * amplitude;
		n = m * n;
		amplitude *= 0.4;
	}
	return total;
}

vec4 mask(vec2 uvPos) {
	vec4 mask = vec4(1.0,1.0,1.0,0);
	if (uvPos.y < 0.5) {
		mask.a = clamp(mask.a + uvPos.y*4.0, 0.0, 1.0);
	}
	else {
		mask.a = clamp(mask.a + (1.0-(uvPos.y-0.5)*2.0)*2.0, 0.0, 1.0);
	}
	return mask;
}

vec4 apply_threshold(vec4 col, vec2 uv) {
	if (col.a > threshold1) {
		if (col.a + (1.0 - uv.y)*0.9 > threshold2) {
			if (col.a + 1.9*(1.0 - uv.y)*0.9 > threshold3) {
				col = vec4(color3.rgb, 1.0);
			}
			else {
				col = vec4(color2.rgb, 1.0);
			}
		}
		else {
			col = vec4(color1.rgb, 1.0);
		}
	}
	else {
		col = vec4(0,0,0,0);
	}
	return col;
}

void fragment() {
	vec2 ps = TEXTURE_PIXEL_SIZE;
    vec2 ratio = vec2(1, ps.x / ps.y);
	vec2 pixels = vec2(float(pixel_amount)) * ratio;
	vec2 grid_uv = round(UV * pixels) * ratio / pixels;
    float time = TIME * speed;
    float q = fbm(grid_uv * cloudscale * 0.5);
    
    //ridged noise shape
	float r = 0.0;
	vec2 uv = grid_uv * cloudscale;
    uv -= q - time;
    float weight = 0.8;
    for (int i=0; i<8; i++){
		r += abs(weight*noise( uv ));
        uv = m*uv + time;
		weight *= 0.7;
    }
    
    //noise shape
	float f = 0.0;
	uv = grid_uv * cloudscale;
    uv -= q - time;
    weight = 0.7;
    for (int i=0; i<8; i++){
		f += weight*noise( uv );
        uv = m*uv + time;
		weight *= 0.6;
    }
    
    f *= r + f;
	
	//noise colour
    float c = 0.0;
    uv = grid_uv * cloudscale * 2.0;
    time = TIME * speed * 2.0;
    uv -= q - time;
    weight = 0.4;
    for (int i=0; i<7; i++){
		c += weight*noise( uv );
        uv = m*uv + time;
		weight *= 0.6;
    }
    
    //noise ridge colour
    float c1 = 0.0;
    uv = grid_uv * cloudscale * 3.0;
    time = TIME * speed * 3.0;
    uv -= q - time;
    weight = 0.4;
    for (int i=0; i<7; i++){
		c1 += abs(weight*noise( uv ));
        uv = m*uv + time;
		weight *= 0.6;
    }
	
    c += c1;
	
	vec4 col = vec4(f) * mask(grid_uv) + 0.5*mask(grid_uv);
	col = apply_threshold(col, grid_uv);

	col = col + 0.05*vec4(c)*col.a;

	COLOR = col;
	//COLOR = mask(grid_uv);
}
"

[sub_resource type="ShaderMaterial" id=5]
shader = SubResource( 4 )
shader_param/pixel_amount = 140
shader_param/cloudscale = 1.949
shader_param/randomness = 29473.7
shader_param/speed = -0.04
shader_param/threshold1 = 0.581
shader_param/color1 = Color( 0.184314, 0.133333, 0.0705882, 1 )
shader_param/threshold2 = 0.625
shader_param/color2 = Color( 0.184314, 0.133333, 0.0705882, 1 )
shader_param/threshold3 = 1.483
shader_param/color3 = Color( 0.364706, 0.207843, 0.113725, 1 )

[node name="Sprite" type="Sprite"]
material = SubResource( 5 )
texture = ExtResource( 1 )
